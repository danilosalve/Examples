#Include "TOTVS.CH"
#include "TBICONN.CH"

/*/{Protheus.doc} DsPrepareStatement
Exemplo de utilização do metodo FwPrepareStatement
@type function
@version 1.0
@author danilo.salve
@since 17/06/2020
@param nLoopMax, Numerico, Valor maximo do Loop
@return aClientes, Array, Array com o nome dos clientes
/*/
Function DsPrepareStatement (nLoopMax as Numeric) as Array

    Local aClientes  := {}
    Local cAlias        := GetNextAlias()
    Local cFinalQuery   as Char
    Local cFilSA1       := xFilial("SA1")
    Local cFilSC5       := xFilial("SC5")
    Local cQuery        as Char
    Local oStatement    as Object
    Local nLoop         := 0
    Local aSelect       := {}
    Local aTamSx3       := TamSx3("A1_VENCLC")

    Default nLoopMax    := 10

    aAdd(aSelect, "A1_NOME")
    aAdd(aSelect, {"A1_VENCLC", aTamSx3[3], aTamSx3[1], aTamSx3[2] })

    cQuery := " SELECT A1_NOME, A1_VENCLC FROM " + RetSqlName("SA1") + " WHERE A1_FILIAL=? AND A1_COD=? AND A1_LOJA=? AND D_E_L_E_T_ = ' ' " 
    oStatement := FWPreparedStatement():New(cQuery)    

    DbSelectArea("SC5")
    SC5->(DbSetOrder(1))    

    While !SC5->(Eof()) .And. SC5->C5_FILIAL == cFilSC5 .And. nLoop++ < nLoopMax

        oStatement:SetString(1, cFilSA1 )
        oStatement:SetString(2, SC5->C5_CLIENTE )
        oStatement:SetString(3, SC5->C5_LOJACLI )
        //Inclui os campos de 
        oStatement:setFields( aSelect )
        //Recupera a consulta já com os parâmetros injetados
        cFinalQuery := oStatement:GetFixQuery()

        dbUseArea(.T.,"TOPCONN",TcGenQry(,,cFinalQuery),cAlias,.T.,.T.)
        //Executa o TcSetfield
        oStatement:doTcSetField( cAlias )

        If !(cAlias)->(Eof())
            aAdd( aClientes, {(cAlias)->A1_NOME, (cAlias)->A1_VENCLC })            
        Endif

        (cAlias)->(DbCloseArea())

        SC5->(DbSkip())

    Enddo

    aSize(aTamSx3, 0)
    SC5->(DbCloseArea())
    oStatement:Destroy()
    
Return aClientes

/*/{Protheus.doc} DsTeste
Função utilizada para testar a função DsPrepareStatement
@type function
@version 1.0
@author danilo.salve
@since 17/06/2020
/*/
User Function DsTeste()

    Local aClientes as Array
    Local nContador as Numeric

    PREPARE ENVIRONMENT EMPRESA 'T1' FILIAL 'D MG 01 '

    Conout("Inicio Query: " + Time())

    aClientes := DsPrepareStatement (900)

    Conout("Fim Query: " + Time())

    If !Empty(aClientes)
        For nContador := 1 to Len(aClientes)
            Conout("Nome do Cliente: " + RTrim(aClientes[nContador,1]) + " - Venc. Lim. Credito: " + DtoC(aClientes[nContador,2]))
        Next nContador
    Endif

    aSize(aClientes,0)

    RESET ENVIRONMENT

Return Nil
